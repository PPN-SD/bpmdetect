#! /usr/bin/env python
import os, sys, glob

if not sys.platform.startswith('win'):
  BOLD   ="\033[1m"
  RED    ="\033[91m"
  GREEN  ="\033[92m"
  BLUE   ="\033[94m"
  YELLOW ="\033[93m" # unreadable on white backgrounds
  CYAN   ="\033[96m"
  NORMAL ="\033[0m"
else:
  BOLD   =""
  RED    =""
  GREEN  =""
  BLUE   =""
  YELLOW =""
  CYAN   =""
  NORMAL =""

#######################
## Source files
#######################
bpmdetect_sources="""
main.cpp
functions.cpp
BPMDetect.cpp
PeakFinder.cpp
FIFOSampleBuffer.cpp
track.cpp
"""
bpmdetect_sources_gui="""
dlgbpmdetectdlg.ui
dlgtestbpmdlg.ui
progressbar.cpp
qdroplistview.cpp
dlgbpmdetect.cpp
dlgtestbpm.cpp
images.cpp
"""

def display_help():
  print """
"""+BOLD+"""  Configure options
--------------------------------------------------"""+NORMAL+"""
"""+BOLD+"""* prefix        """+NORMAL+""": installation prefix (/usr/local)
"""+BOLD+"""* console       """+NORMAL+""": build only console mode
"""+BOLD+"""* mingw         """+NORMAL+""": use MinGW compiler
"""+BOLD+"""* debug         """+NORMAL+""": enable debug (debug=1 or debug=full)
"""+BOLD+"""* extraincludes """+NORMAL+""": extra includes separated by colon (':')

"""+BOLD+"""* qtdir         """+NORMAL+""": where the QT3 is installed
"""+BOLD+"""* qtincludes    """+NORMAL+""": path to QT3 includes (/usr/include/qt3 on debian, ...)
"""+BOLD+"""* qtlibs        """+NORMAL+""": path to QT3 libraries (/usr/lib, ...)
"""+NORMAL
  return

import re
def makeHashTable(args):
  table = { }
  for arg in args:
    if len(arg) > 1:
      lst=arg.split('=')
      if len(lst) < 2:
        continue
      key=lst[0]
      value=lst[1]
      if len(key) > 0 and len(value) >0:
        table[key] = value
  return table

def check_pkgconfig(env):
  print "Checking for pkg-config           : ",

  pkg_config = env.WhereIs("pkg-config").strip()
  if not pkg_config:
    pkg_config = ""
  if len(pkg_config):
    print GREEN+pkg_config+NORMAL,
    pkgcver = os.popen("pkg-config --version").read().strip()
    print BOLD+' version '+pkgcver+NORMAL
    env['PKGCONFIG'] = 1
  else:
    print RED+'not found'+NORMAL
    env['PKGCONFIG'] = 0

def check_taglib(env):
  if env['PKGCONFIG']:
    print "Checking for taglib               : ",
    tagprefix = os.popen("pkg-config --variable=prefix taglib").read().strip()
    if len(tagprefix):
      tagver = os.popen("pkg-config --modversion taglib").read().strip()
      print GREEN+"found "+NORMAL+BOLD+"version "+tagver+NORMAL
      # includes
      print "taglib includes                   : ",
      tagincludes = os.popen('pkg-config --cflags-only-I taglib').read().strip()[2:]
      print BOLD+tagincludes+NORMAL
      # libs
      print "taglib library                    : ",
      taglibpath = os.popen('pkg-config --libs-only-L taglib').read().strip()[2:]
      if not len(taglibpath):
        taglibpath = tagprefix + '/lib'
      print BOLD+taglibpath+NORMAL

      env.Append(CPPDEFINES = 'HAVE_TAGLIB')
      env.Append(CPPPATH = tagincludes)
      env.Append(LIBS = ['tag', 'z'])
      env.Append(LIBPATH = taglibpath)
    else:
      print RED+"not found"+NORMAL
  else:
    print RED+"not found"+NORMAL
    # TODO: check without pkg-config

def configure(env):
  # unset existing variables
  if env.has_key('PREFIX'):
    env.__delitem__('PREFIX')
  if env.has_key('EXTRAINCLUDES'):
    env.__delitem__('EXTRAINCLUDES')
  if env.has_key('ISCONFIGURED'):
    env.__delitem__('ISCONFIGURED')
  if env.has_key('PKGCONFIG'):
    env.__delitem__('PKGCONFIG')
  if env.has_key('MINGWBUILD'):
    env.__delitem__('MINGWBUILD')
  if env.has_key('CONSOLEONLY'):
    env.__delitem__('CONSOLEONLY')
  if env.has_key('CPPPATH'):
    env.__delitem__('CPPPATH')
  if env.has_key('CPPDEFINES'):
    env.__delitem__('CPPDEFINES')
  if env.has_key('CXXFLAGS'):
    env.__delitem__('CXXFLAGS')
  if env.has_key('LIBPATH'):
    env.__delitem__('LIBPATH')
  if env.has_key('LIBS'):
    env.__delitem__('LIBS')
  if env.has_key('LINKFLAGS'):
    env.__delitem__('LINKFLAGS')

  print BOLD + """
--------------------------------------------------""" + NORMAL
  print "Use MinGW compiler                : ",
  if env['ARGS'].get('mingw', None) or sys.platform.startswith('win'):
    env['MINGWBUILD'] = 1
    print BOLD+'yes'+NORMAL
    env.Tool('crossmingw')
    env.Append(LIBS = ['kkeramikstyle','kdefx'])
    # FIXME
    print os.popen('windres -Isrc -i src/icon.rc -o build/icon.o').read()
    env.Append(LINKFLAGS = 'build/icon.o')
    env.Append(LINKFLAGS = '-mwindows')
  else:
    env['MINGWBUILD'] = 0
    print 'no'

  print 'Enable debug                      : ',
  if env['ARGS'].get('debug', None):
    debuglevel = env['ARGS'].get('debug', None)
    print BLUE + 'yes ' + NORMAL + debuglevel
    env.Append(CPPDEFINES = 'DEBUG')
    if (debuglevel == "full"):
      env.Append(CXXFLAGS   = '-g3')
    else:
      env.Append(CXXFLAGS   = '-g')
  else:
    print 'no '
    env.Append(CXXFLAGS = '-O2' )
    env.Append(CPPDEFINES = ['NDEBUG', 'NO_DEBUG'])

  check_pkgconfig(env)

  # User-specified prefix
  if env['ARGS'].get('prefix', None):
    env['PREFIX'] = env['ARGS'].get('prefix', None)
  else:
    if sys.platform.startswith('win'):
      env['PREFIX'] = '../install'
    else:
      env['PREFIX'] = '/usr/local'
  print 'installation prefix               :  '+BOLD + env['PREFIX'] +NORMAL

  # Console mode only
  print 'GUI                               : ',
  if env['ARGS'].get('console', None):
    env['CONSOLEONLY'] = 1
    print BLUE+'disabled'+NORMAL
  else:
    env['CONSOLEONLY'] = 0
    print GREEN+'enabled'+NORMAL

  # User-specified include paths
  env['EXTRAINCLUDES'] = env['ARGS'].get('extraincludes', None)
  if env['ARGS'].get('extraincludes', None):
    incpaths = []
    for dir in str(env['EXTRAINCLUDES']).split(':'):
      incpaths.append( dir )
    env.Append(CPPPATH = incpaths)
  elif env.has_key('EXTRAINCLUDES'):
    env.__delitem__('EXTRAINCLUDES')

  # Check for libraries
  check_taglib(env)

  env.Append(LIBS = ['id3', 'fmodex', 'z'])
  env.Append(CPPPATH = ['.', '..', '../includes'])
  env.Append(LIBPATH = ['.', '../libs'])
  env['ISCONFIGURED']=1
  # And finally save the options in the cache
  opts.Save(cachefile, env)

def install(env, binary_files):
  if not os.path.isdir(env['PREFIX']):
    os.mkdir(env['PREFIX'])

  prefix = env['PREFIX']
  dotdesktop_files = glob.glob('../src/bpmdetect.desktop')
  icon_files = glob.glob('../src/bpmdetect-icon.png')

  if sys.platform.startswith('win'):
    #dlls = env.Install(prefix, dll_files)
    binary = env.Install(prefix, binary_files)

    #env.Alias('install', dlls)
    env.Alias('install', binary)
  else:
    bin_path   = prefix + "/bin"
    icon_path = prefix + "/share/pixmaps"
    dotdesktop_path = prefix + '/share/applications'

    binary = env.Install(bin_path, binary_files)
    dotdesktop = env.Install(dotdesktop_path, dotdesktop_files)
    icon = env.Install(icon_path, icon_files)

    env.Alias('install', binary)
    env.Alias('install', dotdesktop)
    env.Alias('install', icon)


#############################
# Main
#############################
env = Environment(ENV = os.environ,
      toolpath=['./', '../admin/'])

env['ARGS']=makeHashTable(sys.argv)
env['HELP']=0
if '--help' in sys.argv or '-h' in sys.argv or 'help' in sys.argv:
  env['HELP']=1

## Global cache directory
## Put all project files in it so a rm -rf cache will clean up the config
if not env.has_key('CACHEDIR'):
  env['CACHEDIR'] =os.getcwd()+ '/../cache/'
if not os.path.isdir(env['CACHEDIR']):
  os.mkdir(env['CACHEDIR'])

## Avoid spreading .sconsign files everywhere - keep this line
env.SConsignFile(env['CACHEDIR']+'/scons_signatures')

# load the options
from SCons.Options import Options, PathOption
cachefile=env['CACHEDIR']+'cache.py'
opts = Options(cachefile)
opts.AddOptions(
  ( 'CACHEDIR', 'cache directory'),
  ( 'PREFIX', 'prefix for installation' ),
  ( 'EXTRAINCLUDES', 'extra include paths for the project' ),
  ( 'ISCONFIGURED', 'project configured' ),
  ( 'PKGCONFIG', 'pkg-config found' ),
  ( 'MINGWBUILD', 'build using mingw compiler' ),
  ( 'CONSOLEONLY', 'build console mode only' ),
  ( 'CPPPATH', 'include directories' ),
  ( 'CPPDEFINES', 'preprocessor definitions' ),
  ( 'CXXFLAGS', 'C++ compiler options' ),
  ( 'LIBPATH', 'library directories' ),
  ( 'LIBS', 'libraries' ),
  ( 'LINKFLAGS', 'linker flags')
)
opts.Update(env)

# to avoid an error message 'how to make target configure... ?'
env.Alias('configure', None)
env.Alias('help', None)

import SCons.Util

if 'configure' in sys.argv:
  env['_CONFIGURE']=1
else:
  env['_CONFIGURE']=0

# configure the environment if needed
if not env['HELP'] and (env['_CONFIGURE'] or not env.has_key('ISCONFIGURED')):
  configure(env)

if env['HELP']:
  display_help()
else:
  if env.has_key('CONSOLEONLY') and env['CONSOLEONLY']:
    env.Append(CPPDEFINES = 'NO_GUI')
  else:
    env.Tool('qt3')
    bpmdetect_sources = bpmdetect_sources_gui + bpmdetect_sources
  print BOLD + """--------------------------------------------------
""" + NORMAL
  bpmdetect_bin = env.Program('bpmdetect', bpmdetect_sources.split())
  if 'install' in sys.argv:
    install(env, bpmdetect_bin)
